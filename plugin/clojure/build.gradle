/*
 *  Copyright 2014 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
apply plugin: 'java'

repositories {
  mavenCentral()
}


apply plugin:'application'

mainClassName = 'com.twosigma.beaker.clojure.Main'

configurations {
  provided
  compile.extendsFrom provided
}

dependencies {
  provided project(':plugin:jvm')
  provided group: 'org.clojure', name: 'clojure', version: '1.7.0'
}

compileJava {
  options.compilerArgs << '-Xlint:deprecation'
  options.compilerArgs << '-Xlint:unchecked'
}

// Fix command-length issue in windows startscript
task pathingJar(type: Jar) {
  appendix = 'pathing'
  manifest {
    attributes("Class-Path": configurations.runtime.collect { it.getName() }.join(' ') + ' ' + jar.archiveName )
  }
}

applicationDistribution.from(pathingJar) {
  into "lib"
}

startScripts {
  doLast {
    def winScriptFile  = file getWindowsScript()
    def winFileText = winScriptFile.text

    // Remove too-long-classpath and use pathing jar instead
    winFileText = winFileText.replaceAll('set CLASSPATH=.*', 'rem CLASSPATH declaration removed.')
    winFileText = winFileText.replaceAll('("%JAVA_EXE%" .* -classpath ")%CLASSPATH%(" .*)', '$1%APP_HOME%\\\\lib\\\\' + pathingJar.archiveName + '$2')

    winScriptFile.text = winFileText
  }
}

if (hasProperty('evalPluginDir')) {
  installDist.into new File(evalPluginDir, 'clojure')
}

if (hasProperty('includeDepsInJar')) {
  build.dependsOn jar
  jar {
    // need to remove META-INF/*.SF to use this.
    from {
       (configurations.compile - configurations.provided).collect { it.isDirectory() ? it : zipTree(it).matching{exclude{it.path.contains('META-INF')} } }
    } {
      exclude "META-INF/*.SF"
      exclude "META-INF/*.DSA"
      exclude "META-INF/*.RSA"
    }
  }
}

task realclean
realclean.dependsOn(clean)
